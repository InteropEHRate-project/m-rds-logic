apply plugin: 'com.android.library'
apply plugin: "maven-publish"
apply plugin: "kotlin-android"
//apply plugin: 'com.android.application'
group = "eu.interopehrate"
version="0.1.3"

//plugins {
//    id 'com.android.library'
//    id 'kotlin-android'
//}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'

    //fhir
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-android:4.1.0'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:4.1.0'

    // retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'

    // test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //anonymization library
    //api "eu.interopehrate:rdsanoni:0.0.2"
    implementation group:'eu.interopehrate', name:'rdsanoni', version: '0.0.4'

    //security library
    implementation group:'eu.interoperhate', name:'mrdssm', version: '0.2.0'

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "rds-simple"
            artifact("build/outputs/aar/$archivesBaseName"+"-release.aar") {
//            artifact("build/outputs/aar/lib-debug.aar") {
                extension "aar"

                // adds dependencies to the generated POM
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Iterate over the dependencies, adding a <dependency> node for each
                    configurations.releaseCompileClasspath.allDependencies.each {
                        if (it.group != null && it.name != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)

                            //If there are any exclusions in dependency
                            if (it.excludeRules.size() > 0) {
                                def exclusionsNode = dependencyNode.appendNode('exclusions')
                                it.excludeRules.each { rule ->
                                    def exclusionNode = exclusionsNode.appendNode('exclusion')
                                    exclusionNode.appendNode('groupId', rule.group)
                                    exclusionNode.appendNode('artifactId', rule.module)
                                }
                            }
                        }
                    }
                }
            }

            // descriptive information to add to the generated POM file
            pom {
                name = 'RDS-LOGIC  Library'
                description = 'Android library (developed by InteropEHRate consortium) for interacting with S-EHR app adopting RDS-LOGIC protocol.'
                url = 'http://www.interopehrate.eu'
            }
        }
    }

    repositories {
        maven {
            name "IEHR-Nexus"
            url "http://213.249.46.206:8081/repository/maven-releases/"
            allowInsecureProtocol = true
            credentials {
                username project.repoUser
                password project.repoPassword
            }
        }
    }
}